/* Copyright zeroRISC Inc. */
/* Modified by Authors of "Towards ML-KEM & ML-DSA on OpenTitan" (https://eprint.iacr.org/2024/1192). */
/* Copyright "Towards ML-KEM & ML-DSA on OpenTitan" Authors. */
/* Modified by Ruben Niederhagen and Hoang Nguyen Hien Pham - authors of */
/* "Improving ML-KEM & ML-DSA on OpenTitan - Efficient Multiplication Vector Instructions for OTBN" */
/* (https://eprint.iacr.org/2025/2028). */
/* Copyright Ruben Niederhagen and Hoang Nguyen Hien Pham. */
/* Licensed under the Apache License, Version 2.0, see LICENSE for details. */
/* SPDX-License-Identifier: Apache-2.0 */

.text

/* Config to start a SHAKE-128 operation. */
#define SHAKE128_CFG 0x2

/*
 * Name: poly_gen_matrix_init
 *
 * Initialze a SHAKE128 operation to prepare for rejection sampling
 * on uniform random bytes using `poly_gen_matrix`.
 *
 * @param[in]  x10: pointer to seed (KYBER_SYMBYTES = 32)
 * @param[in]  x11: pointer to i||j (2 bytes)
 *
 * clobbered registers: x5, w0
 * clobbered flag groups: none
 */

.globl poly_gen_matrix_init
.type poly_gen_matrix_init, @function
poly_gen_matrix_init:
  /* Initialize a SHAKE128 operation. */
  addi  x5, x0, 34
  slli  x5, x5, 5
  addi  x5, x5, SHAKE128_CFG
  csrrw x0, kmac_cfg, x5

  /* Send the message to the Keccak core. */
  bn.lid  x0, 0(x10)
  bn.wsrw kmac_msg, w0
  li      x5, 2
  csrrw   x0, kmac_partial_write, x5
  bn.lid  x0, 0(x11)
  bn.wsrw kmac_msg, w0
  ret

/*
 * Name: poly_gen_matrix
 *
 * Run rejection sampling on uniform random bytes to generate
 * 256 uniform random integers mod q = 3329; this function
 * assumes `poly_gen_matrix_init` has been called first with
 * the appropriate seed and indices.
 *
 * @param[out] x11: dmem pointer to polynomial
 * @param[in]  kmac_digest: SHAKE-128 squeeze set up by poly_gen_matrix_init
 * @param[in]  w31: all-zero register
 *
 * clobbered registers: x4 to x7, x11, x28, w0 to w6
 * clobbered flag groups: FG0
 */

.globl poly_gen_matrix
.type poly_gen_matrix, @function
poly_gen_matrix:
  /* dptr_r + 512 is the last valid address. */
  addi x5, x11, 512

  #define accumulator w0
  #define coeff_mask w1
  #define cand w2
  #define mod w3
  #define wtmp w4
  #define accumulator_new w5
  #define shake_reg w6
  #define accumulator_count x6
  #define cand_count x7

  /* Generate constant 0x0fff. */
  bn.addi coeff_mask, w31, 1
  bn.rshi coeff_mask, coeff_mask, w31 >> 244
  bn.subi coeff_mask, coeff_mask, 1

  /* Load modulus. */
  li      x4, 3
  la      x6, modulus_bn
  bn.lid  x4, 0(x6)
  bn.rshi mod, w31, mod >> 240 /* Only keep mod in lowest word */

  /* Counts number of remaining accumulator slots. */
  li accumulator_count, 16

  /* Loop until 256 coefficients have been written to the output. */
_rej_sample_loop:
  /* With one SHAKE squeeze, we get 32 bytes of data. From this, we can try to
   * build 20 coefficients with 3 bytes each two (3 bytes --> 2 coeffs) and are left with 2 bytes
   * remainder. We then take the two remaining bytes and one byte from the
   * next squeeze operation and try to get another 2 coefficient, leaving us
   * with 31 bytes from which we can, again, try to read 20 coefficients and
   * are left with 1 byte remainder. From the next 32 bytes, we take 2 bytes
   * and try to build 2 coefficients with the remaining 1 byte. Finally, we
   * are left with 30 bytes which we can try to turn into 20 coefficients
   * without any remainder. lcm(3, 32) = 96, meaning we use 96 bytes of SHAKE
   * output each (full) iteration of the main loop. In case we reach the
   * target amount of coefficients, we jump to _end_rej_sample_loop and exit. */

  bn.wsrr    shake_reg, kmac_digest
  jal        x1, _poly_uniform_inner_loop
  beq        x11, x5, _end_rej_sample_loop

  /* 2 bytes of first squeeze + 1 byte of second squeeze. */
  bn.rshi    cand, shake_reg, w31 >> 16
  bn.wsrr    shake_reg, kmac_digest
  bn.rshi    cand, shake_reg, cand >> 240
  bn.rshi    shake_reg, w31, shake_reg >> 8

  bn.and     wtmp, coeff_mask, cand
  bn.cmp     wtmp, mod
  csrrs      x28, fg0, x0 /* Read flags. */
  andi       x28, x28, 1  /* Mask carry flag to detect underflow. */
  bn.rshi    accumulator_new, wtmp, accumulator >> 16
  bn.sel     accumulator, accumulator_new, accumulator, FG0.C
  sub        accumulator_count, accumulator_count, x28 /* Move to next slot iff not rejected. */
  bne        accumulator_count, x0, _skip_store2a
  bn.sid     x0, 0(x11++)
  li         accumulator_count, 16 /* Set all slots to available. */
  beq        x11, x5, _end_rej_sample_loop

_skip_store2a:
  bn.rshi    cand, w31, cand >> 12
  bn.and     cand, coeff_mask, cand
  bn.cmp     cand, mod
  csrrs      x28, fg0, x0
  andi       x28, x28, 1
  bn.rshi    accumulator_new, cand, accumulator >> 16
  bn.sel     accumulator, accumulator_new, accumulator, FG0.C
  sub        accumulator_count, accumulator_count, x28
  bne        accumulator_count, x0, _skip_store2
  bn.sid     x0, 0(x11++)
  li         accumulator_count, 16
  beq        x11, x5, _end_rej_sample_loop

_skip_store2:
  jal        x1, _poly_uniform_inner_loop
  beq        x11, x5, _end_rej_sample_loop

  /* 1 byte of second squeeze + 2 bytes of third squeeze. */
  bn.rshi    cand, shake_reg, w31 >> 8
  bn.wsrr    shake_reg, kmac_digest
  bn.rshi    cand, shake_reg, cand >> 248
  bn.rshi    shake_reg, w31, shake_reg >> 16

  bn.and     wtmp, coeff_mask, cand
  bn.cmp     wtmp, mod
  csrrs      x28, fg0, x0
  andi       x28, x28, 1
  bn.rshi    accumulator_new, wtmp, accumulator >> 16
  bn.sel     accumulator, accumulator_new, accumulator, FG0.C
  sub        accumulator_count, accumulator_count, x28
  bne        accumulator_count, x0, _skip_store4a
  bn.sid     x0, 0(x11++)
  li         accumulator_count, 16
  beq        x11, x5, _end_rej_sample_loop

_skip_store4a:
  bn.rshi    cand, w31, cand >> 12
  bn.and     cand, coeff_mask, cand
  bn.cmp     cand, mod
  csrrs      x28, fg0, x0
  andi       x28, x28, 1
  bn.rshi    accumulator_new, cand, accumulator >> 16
  bn.sel     accumulator, accumulator_new, accumulator, FG0.C
  sub        accumulator_count, accumulator_count, x28
  bne        accumulator_count, x0, _skip_store4
  bn.sid     x0, 0(x11++)
  li         accumulator_count, 16
  beq        x11, x5, _end_rej_sample_loop

_skip_store4:
  jal        x1, _poly_uniform_inner_loop
  beq        x11, x5, _end_rej_sample_loop
  beq        x0, x0, _rej_sample_loop

_end_rej_sample_loop:
  ret

_poly_uniform_inner_loop:
  /* Skip the per-iteration total coefficient count checks in this hot loop if
   * we have more than 20 candidates remaining. */
  sub  x7, x11, x5               /* Get -(number of bytes remaining to write out). */
  addi x7, x7, 64                /* Add 64 bytes = 2 wide words >= 20 coeffs. */
  sra  x7, x7, 31                /* Fill register with resulting sign bit. */
  bne  x7, x0, _fast_inner_loop  /* _fast_inner_loop skips checks of x5. */

  loopi 20, 12
    beq      x11, x5, _skip_store1
    bn.and   cand, coeff_mask, shake_reg
    bn.cmp   cand, mod
    csrrs    x28, fg0, x0
    andi     x28, x28, 1
    bn.rshi  accumulator_new, cand, accumulator >> 16
    bn.sel   accumulator, accumulator_new, accumulator, FG0.C
    sub      accumulator_count, accumulator_count, x28
    bne      accumulator_count, x0, _skip_store1
    bn.sid   x0, 0(x11++)
    li       accumulator_count, 16
  _skip_store1:
    bn.rshi  shake_reg, w31, shake_reg >> 12 /* Shift out used 12 bits. */
  endloop
  ret

_fast_inner_loop:
  /* Eagerly fill the accumulator (fine since 16 < 20). */
  li  cand_count, 20
  sub cand_count, cand_count, accumulator_count
  loop accumulator_count, 8
    bn.and     cand, coeff_mask, shake_reg
    bn.cmp     cand, mod
    csrrs      x28, fg0, x0
    andi       x28, x28, 1
    bn.rshi    accumulator_new, cand, accumulator >> 16
    bn.sel     accumulator, accumulator_new, accumulator, FG0.C
    sub        accumulator_count, accumulator_count, x28
    bn.rshi    shake_reg, w31, shake_reg >> 12
  endloop

  /* Possibly flush accumulator if we filled it (~3% of time). */
  bne    accumulator_count, x0, _handle_rest
  bn.sid x0, 0(x11++)
  li     accumulator_count, 16

_handle_rest:
  loop cand_count, 11
    bn.and     cand, coeff_mask, shake_reg
    bn.cmp     cand, mod
    csrrs      x28, fg0, x0
    andi       x28, x28, 1
    bn.rshi    accumulator_new, cand, accumulator >> 16
    bn.sel     accumulator, accumulator_new, accumulator, FG0.C
    sub        accumulator_count, accumulator_count, x28
    bne        accumulator_count, x0, _skip_store1_fast
    bn.sid     x0, 0(x11++)
    li         accumulator_count, 16
  _skip_store1_fast:
    bn.rshi    shake_reg, w31, shake_reg >> 12
  endloop
  ret
